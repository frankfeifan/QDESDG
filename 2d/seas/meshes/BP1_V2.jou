# RUN WITH CUBIT 15.2

# Set units to SI.
${Units('si')}

#---------------
reset

${Lx=100}
${H=15}
${h=3}
${W=40}
${Ly=Lx}

${d0=0}
${d4=0}

${a=3}

create vertex    {-Lx}       0  0 #v1
create vertex    {+Lx}       0  0 #v2
create vertex    {-Lx}   {-Ly}  0 #v3
create vertex    {+Lx}   {-Ly}  0 #v4
create vertex       0        0  0 #v5
create vertex       0     {-H}  0 #v6
create vertex       0   {-H-h}  0 #v7
create vertex       0     {-W}  0 #v8
create vertex       0    {-Ly}  0 #v9

#  1--5--2
#  |  |  |
#  |  6  |
#  |  |  |
#  |  7  |
#  |  |  |
#  |  8  |
#  |  |  |
#  3--9--4

# Create outer box
create curve vertex  1 3 #c1
create curve vertex  2 4 #c2
create curve vertex  1 2 #c3
create curve vertex  3 4 #c4
create surface curve 1 2 3 4

# Create Fault
create curve vertex  5 6 #c5
create curve vertex  6 7 #c6
create curve vertex  7 8 #c7
create curve vertex  8 9 #c8

imprint tolerant surface 1 with curve 5 6 7 8 merge
delete curve 5 6 7 8

surface 2 3 size {a}
mesh surface 2 3

refine curve 10 11 12 numsplit 1 bias 1.0 depth 2 smooth
refine curve 10 11 12 numsplit 1 bias 1.0 depth 2 smooth
refine curve 10 11 12 numsplit 3 bias 1.0 depth 2 smooth

Sideset 1 add curve 1 2         # Displacement (half V_p)
Sideset 2 add curve 13 14 15 16 # Free
Sideset 3 add curve 9           # Displacement jump with V_p
Sideset 7 add curve 10 11 12    # Frictional fault

export Abaqus "/Users/jekozdon/Dropbox/Writings/2017_QDES_DG/matlab/2d/seas/meshes/BP1_V2.inp"  overwrite
